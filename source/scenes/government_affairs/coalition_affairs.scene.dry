title: 执政联盟
new-page: true
is-card: true
tags: govt_affairs
on-arrival: month_actions += 1; coalition_affairs_timer = 5
view-if: spd_in_government = 1 and coalition_dissent >= 1 and coalition_affairs_timer = 0 and (in_grand_coalition or in_weimar_coalition or in_popular_front)
card-image: img/muller_cabinet.jpg

= 执政联盟

我们的执政联盟有麻烦了。右倾的中央党[? if in_grand_coalition: 和人民党 ?]成员不断批评我们的政策，并威胁发起不信任动议让政府[? if spd_prussia :或者我们在普鲁士的州政府 ?]垮台。

我们要如何缓解联盟中的紧张局势呢？

- @promise_cuts: 同意资产阶级削减福利的计划。
- @resources: 用党内资源来安抚我们的盟友。
- @bring_down: 我们不如自己发起不信任动议让政府垮台，并举行新大选。
- @no_change: 不做以上的任何行动。
- @easy_discard


@promise_cuts
on-arrival: workers_spd -= 5; unemployed_spd -= 6; workers_kpd += 5; unemployed_kpd += 3; unemployed_nsdap += 3; left_dissent += 10; labor_dissent += 5; welfare -= 1; budget += 1; coalition_dissent = 0

好吧。我们通过削减福利减少了联盟内的不满。

@resources
subtitle: -2资源
unavailable-subtitle: Not enough resources (need 2).
choose-if: resources >= 2
on-arrival: resources -= 2; coalition_dissent -= 1

我们可以通过将宣传经费送给盟友来使他们更能接受我们的政策。有些人会称之为腐败，但也许这对于拯救民主来说是必要的。

@no_change

不，我们什么都不会做。

@bring_down
subtitle: 在我们的对手行动之前动手。
on-arrival: spd_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; chancellor = "Brüning"; chancellor_party = "Z"; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_toleration = 0; left_strength += 8; pro_republic -= 5; new_middle_spd -= 2; rural_spd -= 2; old_middle_spd -= 3; catholics_spd -= 2; reformist_dissent += 5

我们对自己发起了不信任动议并使政府垮台。[+ chancellor +]目前代为行使总理职能。

新一轮大选于[+ next_election_year +]年[+ next_election_month : month +]举行。

