title: 经济政策
new-page: true
is-card: true
tags: govt_affairs
on-arrival: economic_policy_timer = 12; month_actions += 1; nationalize_budget = 4; nationalize_budget -= 1 if works_councils >= 2; nationalize_budget -= 1 if works_councils >= 3; nationalize_budget -= 1 if socializations + nationalization_progress >= 2
view-if: spd_in_government = 1 and (economic_minister_party == "SPD" or finance_minister_party == "SPD") and economic_policy_timer == 0 and economic_plan > 0
card-image: img/portraits/SchmidtRobert.jpg

= Economic Policy

我们已经制定了一项经济政策，现在是实行的时候了。

- @wtb_2
- @wtb_2_deficit
- @wtb_continuation
- @implement_wtb_no_deficit
- @implement_wtb_deficit
- @nationalize_1
- @nationalize_2
- @root: Do not implement the economic plan.
- @easy_discard

@wtb_2
title: 继续实行WTB政策。
subtitle: -3预算
unavailable-subtitle: 失业率已经足够低了，已经不需要WTB政策了。
view-if: wtb_implemented > 2 and budget >= 3 
choose-if: unemployed > 8
on-arrival: works_program += 1; budget -= 3; workers_spd += 7*(1-dissent); workers += 2 if unemployed > 8; unemployed -= 3 if unemployed > 8; unemployed_spd += 7*(1-dissent); workers_kpd -= 3; workers_nsdap -= 3; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front

[? if works_program == 4 : 我们开始了WTB政策的第二阶段。 ?][? if works_program > 4 :  我们继续实行了WTB政策。 ?]除了公共工程外，我们现在正集中精力重建我们的工业。


@wtb_2_deficit
title: 继续实行WTB政策。
subtitle: -3预算 - 我们会陷入赤字。
unavailable-subtitle: 失业率已经足够低了，我们不应该进行赤字开支。
view-if: wtb_implemented > 2 and budget < 3 and budget >= -3
choose-if: unemployed > 10
on-arrival: works_program += 1; budget -= 3; workers_spd += 6*(1-dissent); workers += 2; unemployed -= 2; unemployed_spd += 6*(1-dissent); workers_kpd -= 2; workers_nsdap -= 2; coalition_dissent += 1; capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front

虽然赤字开支还在继续，我们继续实行了WTB政策。除了公共工程外，我们现在正集中精力重建我们的工业。


@wtb_continuation
title: 回顾我们的成果，继续实行WTB政策……
view-if: wtb_implemented >= 1 and wtb_implemented <= 2
on-arrival: budget += 1 if works_program <= 2; unemployed -= 5 if (works_program <= 2 and unemployed > 15) else unemployed -= 3; unemployed += 3 if unemployed < 5; unemployed += 3 if unemployed < 0; workers += 4 if works_program <= 2 else workers += 2; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); workers_kpd -= 3; workers_nsdap -= 5; works_program += 1; z_relation += 3; dvp_relation += 3; coalition_dissent -= 1 if coalition_dissent >= 1; wtb_implemented += 1; capital_strike_progress -= 1 if capital_strike_progress >= 3;

我们在公共工程的支出降低了对失业金和福利的需求，同时随着新货币在市场的流通，我们的税收在相乘效应下增加了，并弥补了我们的部分支出。我们继续将大部分新收入注入这项工程。

经济终于好转了。就连资产阶级政党也开始看到了希望。

[? if in_popular_front or in_left_front: 然而，共产党仍然反对我们给资本主义“急救”的政策。 ?]

@implement_wtb_no_deficit
title: 实行WTB政策。
subtitle: -3预算
unavailable-subtitle: 失业率已经足够低了，已经不需要WTB政策了。
view-if: budget >= 3 and works_program == 0 and wtb_adopted == 1
choose-if: unemployed > 8
on-arrival: budget -= 3; works_program += 1; unemployed -= 3; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front
achievement: wirtschaftspolitik

我们开始实行WTB政策。货币开始通过对大型公共工程的投资在市场流通。

[? if in_popular_front or in_left_front: 共产党反对WTB政策，他们认为这是在给资本主义做急救而不是向真正的社会主义迈进。 ?]

@implement_wtb_deficit
title: 实行WTB政策。
subtitle: -3预算 - 我们会陷入赤字。
unavailable-subtitle: 失业率已经足够低了，已经不需要WTB政策了。
view-if: budget < 3 and works_program == 0 and wtb_adopted == 1
choose-if: unemployed > 8
on-arrival: coalition_dissent += 1; works_program += 1; budget -= 3; unemployed -= 3; workers += 3; workers_spd += 8*(1-dissent); unemployed_spd += 8*(1-dissent); capital_strike_progress += 1; wtb_implemented += 1; kpd_coalition_dissent += 1; kpd_relation -= 5 if in_popular_front or in_left_front
achievement: wirtschaftspolitik

我们开始进行赤字开支。我们的盟友对此很反感，但公共工程计划还是颁布了。货币开始通过对大型公共工程的投资在市场流通。

[? if in_popular_front or in_left_front: 共产党反对WTB政策，他们认为这是在给资本主义做急救而不是向真正的社会主义迈进。 ?]


####################### left economic policies (socializations)

@nationalize_1
title: 实行左派的经济转型计划。
subtitle: -[+ nationalize_budget +]预算。[? if works_councils >= 3 : 我们的工人委员会降低了实行这个政策所需的预算。 ?] [? if socializations >= 2: 我们既有的社会化政策降低了实行这个政策所需的预算。 ?]
view-if: nationalization_adopted == 1 and nationalization_progress == 0
go-to: nationalize_deficit if budget < nationalize_budget; nationalize_no_deficit if budget >= nationalize_budget


@nationalize_no_deficit
on-arrival: nationalization_progress += 1; capital_strike_progress += 2; unemployed -= 3 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; workers += 2.5; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 4*(1-dissent); coalition_dissent += 1; dvp_relation -= 4; left_dissent -= 5; new_middle_spd -= 2; old_middle_spd -= 3; economy_goal_completed += 1;
achievement: wirtschaftspolitik

[? if nationalization_progress == 1 : 我们开始从资本家手中购置闲置企业[? if works_councils >= 1: 并与工人委员会联合经营 ?]。通过保持生产，我们可以避免失业率上升。 ?]

[? if nationalization_progress == 1 : 左派认为这只应该作为向社会主义经济转型的第一步。整个经济的一大部分最终必须由人民控制。 ?]

[? if nationalization_progress > 1 : 我们购买了更多闲置的企业并与工人委员会一起以社会主义的方式运营它们，并雇佣了之前下岗的工人。经济的一大部分现在已处于公共控制之下。 ?]

@nationalize_deficit

不幸的是，我们没有开始收购工业设施的预算。有些人催促我们为购置工厂进行赤字开支，另外一些人则鼓励工人接手工厂（目前并不合法），或者不对资本家进行补偿。不管采取上述的哪一个行动，都大大偏离了我们之前的立场。

- @nationalize_deficit_1
- @nationalize_without_compensation
- @empower_workers
- @return: No, we should not do this.


@nationalize_deficit_1
title: 为实行国有化政策进行赤字开支。
choose-if: left_strength > reformist_strength + center_strength or woytinsky_advisor == 1
subtitle: [? if woytinsky_advisor: 沃伊京斯基可以让我们支持赤字开支。 ?]
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: 党内尚不支持为此进行赤字开支。 ?] [? if not woytinsky_advisor : 也许请一位顾问能让这一行动更好接受？ ?]
on-arrival: nationalization_progress += 1; capital_strike_progress += 3; unemployed -= 2.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; budget -= nationalize_budget; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 4*(1-dissent); coalition_dissent += 2; dvp_relation -= 6; z_relation -= 4; new_middle_spd -= 3; old_middle_spd -= 3; economy_goal_completed += 1;
achievement: wirtschaftspolitik

[? if woytinsky_advisor: 党内的大多数，甚至包括左派，都反对为任何目的进行赤字开支，只有弗拉基米尔·沃伊京斯基支持。虽然他一开始并不支持左派的“转型”政策，他现在已经改变了主意，并说服党和财政部在有限的情况下支持赤字开支，以摆脱经济萧条。 ?]

[? if nationalization_progress == 1 : 我们开始从资本家手中购置闲置企业[? if works_councils >= 1: 并与工人委员会联合经营 ?]。通过保持生产，我们可以避免失业率上升。 ?]

[? if nationalization_progress == 1 : 左派认为这只应该作为向社会主义经济转型的第一步。整个经济的一大部分最终必须由人民控制。 ?]

[? if nationalization_progress > 1 : 我们购买了更多闲置的企业并与工人委员会一起以社会主义的方式运营它们，并雇佣了之前下岗的工人。经济的一大部分现在已处于公共控制之下。 ?]

@nationalize_without_compensation
title: 不对资本家进行补偿。
subtitle: 并没有什么理由阻止我们直接接手闲置的工厂。这一行为很可能会遭到资本家的抵抗。
unavailable-subtitle: [? if left_strength <= reformist_strength + center_strength: 党内的大多数并不支持这一游走于法律边缘的行为。 ?] [? if judicial_reform < 3 : 司法绝不会同意的。 ?]
choose-if: left_strength > reformist_strength + center_strength and judicial_reform >= 3
on-arrival: nationalization_progress += 1; capital_strike_progress += 7; coup_progress += 3; unemployed -= 2.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3 and unemployed >= 3; works_program += 1; workers_spd += 8*(1-dissent); unemployed_spd += 6*(1-dissent); coalition_dissent += 2; dvp_relation -= 10; z_relation -= 8; ddp_relation -= 8; left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 5; old_middle_spd -= 8; economy_goal_completed += 1; kpd_relation += 5*(1-dissent);
achievement: wirtschaftspolitik

The capitalists are, predictably, up in arms about our policy of nationalization without compensation. They call it illegal expropriation, and it is. Nevertheless, it is clear that they have failed by keeping their factories idle, and we must take radical action.

[? if nationalization_progress == 1 : We have begun acquiring idling enterprises from the capitalists[? if works_councils >= 1: , and running them in coordination with empowered works councils ?]. By keeping production going, we can stave off the rising unemployment. ?]

[? if nationalization_progress > 1 : We acquired additional underutilized enterprises and run them in a coordinated socialist fashion with the works councils, hiring formerly laid off workers. A significant portion of the economy is now under public control. ?]

@empower_workers
title: Empower workers to take over factories.
subtitle: We can promise to legalize factory occupations, giving the factories over to worker unions. This is highly confrontational, and would likely provoke a reaction from right-wing militants as well as capitalists.
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : The bulk of the party does not support this radical action. ?] [? if judicial_reform < 3 : The judiciary would oppose this action. ?] [? if rb_militancy < 0.25 : If we increased the strength of our paramilitary forces, perhaps we could do this. ?] [? if works_councils < 1 : We need the support of the works councils first. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and (judicial_reform >= 3 or rb_militancy >= 0.25) and works_councils >= 1
on-arrival: nationalization_progress += 1; capital_strike_progress += 6; coup_progress += 5; unemployed -= 2 if unemployed >= 6; unemployed -= 1.5 if works_councils >= 3 and unemployed > 4; works_program += 1; workers_spd += 10*(1-dissent); unemployed_spd += 10*(1-dissent); coalition_dissent += 2; dvp_relation -= 10; z_relation -= 8; ddp_relation -= 8; workers_nsdap -= 5*(1-dissent); unemployed_nsdap -= 5*(1-dissent); left_strength += 15; reformist_dissent += 10; left_dissent -= 10; new_middle_spd -= 5; old_middle_spd -= 10; factory_takeovers += 1; economy_goal_completed += 1; kpd_relation += 5*(1-dissent);
achievement: wirtschaftspolitik

The capitalists are, predictably, up in arms about our policy of legalizing factory occupations. They call it illegal expropriation, and it is. Nevertheless, it is clear that they have failed, and we must take radical action.

Fascists have been taking increasingly violent actions around the occupied factories, calling us thieves and looters. However, the some of the working-class supporters of the fascists have been peeled off, or at least the ones with a sense of class solidarity.


@nationalize_2
title: Continue the Left plan for the transformation of the economy.
subtitle: -[+ nationalize_budget +] budget. [? if works_councils >= 3 : Our works councils have reduced the budget necessary for this program. ?] [? if socializations + nationalization_progress >= 2: Our existing socialization policies have reduced the budget necessary for this program. ?]
view-if: nationalization_adopted == 1 and nationalization_progress >= 1
go-to: nationalize_deficit if budget < nationalize_budget; nationalize_no_deficit if budget >= nationalize_budget
