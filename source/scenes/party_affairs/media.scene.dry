title: 媒体
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: month_actions += 1; media_timer = 6;
card-image: img/sangerbund.jpg
max-visits: 6

= 媒体

社民党经营着一个名副其实的媒体帝国，多家报纸覆盖了德国的每个城市与区域。我们可以用媒体做许多事情……

- @commercial
- @campaign
- @ideological
- @radio
- @radio_2
- @radio_3
- @return: 我们现在不该拿媒体做任何事情。
- @easy_discard

@commercial
title: 通过将报纸的诉求扩张到票仓之外来使我们的报纸更加商业化。
subtitle: 这可能会让意识形态洁癖者不高兴，但这可能会带来更加经费，并使中产阶级接触到社会主义观点。
on-arrival: resources += 1; commercialized_media += 1; new_middle_spd += 4*(1 - dissent); old_middle_spd += 3*(1-dissent); left_dissent += 10; labor_dissent += 10

我们的商业化报纸带来了更多经费，然而左派与劳工派表示了不满。

@campaign
title: 我们媒体的目标是使更多社会民主主义者当选。
subtitle: 我们的报纸将注重选举宣传。
on-arrival: campaign_media += 1; workers_spd += 4*(1 - dissent); unemployed_spd += 2*(1-dissent);

我们的宣传媒体主要接触的是已很熟悉社会主义一套的工人，但它必须得*做点什么*，至少我们是这么期望的。

@ideological
title: 我们将在报纸中展开活跃的意识形态辩论。
subtitle: 所有派别将有空间发表他们的言论。
on-arrival: left_dissent -= 8; labor_dissent -= 8; center_dissent -= 8; reformist_dissent -= 8; neorevisionist_dissent -= 8

这曾经是我们的媒体政策，并成功地缓解了我们党内的紧张局势。

@radio
title: 建一个电台怎么样？
subtitle: -2资源
choose-if: resources >= 2
unavailable-subtitle: 资源不足。
view-if: radio = 0
on-arrival: radio += 1; resources -= 2; new_middle_spd += 4*(1 - dissent); old_middle_spd += 3*(1 - dissent); workers_spd += 2*(1 - dissent); labor_dissent += 5; socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent) if pacifism > 1; pro_republic += 3*(1-dissent) if democratization > 1

我们的新电台将社会主义信息跟音乐和其他流行内容结合，这使我们能够更紧密地与人接触。然而，使我们劳工派同志感到痛苦的是，因为大多数无产阶级家庭买不起电台，我们针对的是中产阶级。

@radio_2
title: 我们必须继续建设广播网络。
subtitle: -1资源
choose-if: resources >= 1
unavailable-subtitle: 资源不足。
view-if: radio > 0 and radio <= 3
on-arrival: radio += 1; resources -= 1; new_middle_spd += 5*(1 - dissent); old_middle_spd += 3*(1 - dissent); workers_spd += 3*(1 - dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

我们的新广播网络同时拥有流行与政治内容，结果很成功。

@radio_3
title: 我们必须继续建设电台。
subtitle: 电台现在可以自给自足，不需要额外经费。
view-if: radio > 3
on-arrival: radio += 1; new_middle_spd += 5*(1 - dissent); old_middle_spd += 4*(1 - dissent); workers_spd += 4*(1 - dissent); rural_spd += 3*(1 - dissent); unemployed_spd += 2*(1-dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

我们的广播网络成为了传播我们信息的成功工具。
